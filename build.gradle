plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'de.dasbabypixel.api'
version '1.0.7'

sourceCompatibility = 8
targetCompatibility = 8

ext.moduleName = 'de.dasbabypixel.property'

sourceSets {
    moduleInfo {
        java {
            srcDir 'src/module-info/java'
        }
    }
}

compileModuleInfoJava {
    sourceCompatibility = 9
    targetCompatibility = 9

    inputs.property("moduleName", moduleName)

    doFirst {
        classpath += sourceSets.main.compileClasspath

        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-SYSTEM',
                '-d', sourceSets.main.output.classesDirs.asPath
        ]
    }
}

jar {
    from sourceSets.main.output
    from sourceSets.moduleInfo.output
}

publishing {
    publications {
        java(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'DarkCube'
            credentials PasswordCredentials
            url = 'https://nexus.darkcube.eu/repository/dasbabypixel/'
        }
    }
}
